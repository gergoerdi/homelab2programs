10 DIM PO(1), PL(1), MP(80)
20 GOSUB 1950
30 PL(0)=97 : PL(1)=99
40 KK = 116
50 LE = 22
60 CX$ = "+" : KX$ = CHR$(43+128) : CV = 149 : CF = 234
70 PRINT CHR$(12);
80 FOR I=1 TO 6:PRINT CX$;" ";:NEXT:PRINT CX$;:FOR I=1 TO 8:PRINT CHR$(CV);:NEXT:PRINT CX$;:FOR I=1 TO 4:PRINT CHR$(CV);:NEXT:PRINT CX$;:FOR I=1 TO 6:PRINT " ";CX$;:NEXT
90 PRINT "    ";CHR$(KK);"     ";CHR$(97);"  ";CHR$(CF);"BEKERITO";CHR$(CF);"2023";CHR$(CF);"  ";CHR$(99);"     ";CHR$(KK)
100 FOR I=1 TO 6:PRINT CX$;" ";:NEXT:PRINT CX$;:FOR I=1 TO 8:PRINT CHR$(CV);:NEXT:PRINT CX$;:FOR I=1 TO 4:PRINT CHR$(CV);:NEXT:PRINT CX$;:FOR I=1 TO 6:PRINT " ";CX$;:NEXT
110 PRINT ""
120 PRINT "A JATEKOT KETTEN JATSZAK: ";CHR$(97);" ES ";CHR$(99);"."
130 PRINT ""
140 PRINT "FELVALTVA LEPNEK, AZAZ KOTNEK OSSZE KET ";
150 PRINT "SZOMSZEDOS PONTOT AZOK KOORDINATAINAK"
160 PRINT "MEGADASAVAL (SOR,OSZLOP). AKI EGY NEGY- ";
170 PRINT "ZET KORE AZ UTOLSO VONALAT HUZZA, EZZEL ";
180 PRINT "BEKERITVE AZT, MEGSZERZI A TERULETET, S ";
190 PRINT "EZERT ";CHR$(43+128);CHR$(49+128);" PONTOT KAP."
200 PRINT "VONALHUZAS HELYETT KINCSET (";CHR$(KK);") IS LETE- ";
210 PRINT "HET VAGY ELASHAT EGY VELETLEN HELYRE."
220 PRINT "(AZ ELASOTT KINCS NEM LATHATO.) AKI A"
230 PRINT "KINCS TERULETET MEGSZERZI, KAP TOVABBI"
240 PRINT CHR$(43+128);CHR$(49+128);" PONTOT. ";
250 PRINT "(MAXIMUM 9 KINCS HASZNALHATO FEL.)"
260 PRINT "HA MINDEN SZABAD TERULET ELFOGY, A JATEK";
270 PRINT "VEGET ER."
280 PRINT "AZ NYER, AKINEK TOBB PONTOTJA LESZ."
290 PRINT ""
300 PRINT ""
310 PRINT "      ";CHR$(CF);"KEZDESHEZ";CHR$(CF);"NYOMD";CHR$(CF);"MEG";CHR$(CF);"A";CHR$(CF);CHR$(195);CHR$(210);CHR$(KK);"GOMBOT";CHR$(CF);
320 RM = 49153+22*40+6
330 POKE RM, 43 : POKE RM+80, 43 : FOR I = RM+1 TO RM+32 STEP 2 : POKE I, CV, 43 : POKE I + 80, CV, 43 : NEXT
340 CU = 0 : RM = RM + 63
350 CU = 128 - CU : POKE RM, 67+CU, 82+CU : A=RND(99) : A = USR(0) : IF A <> 13 THEN GOTO 350
360 PRINT CHR$(12)
370 MK = 9
380 FOR I = 0 TO 80 : MP( I ) = 0 : NEXT
390 FOR X = 0 TO 9
400 CO=X*2 : RO = 0 : GOSUB 1970 : PRINT X; : RO = 20 : GOSUB 1970 : PRINT X;
410 NEXT
420 FOR Y = 0 TO 9
430 CO=0 : RO = 1+Y*2 : GOSUB 1970 : PRINT CHR$(65+Y); : CO=20 : GOSUB 1970 : PRINT CHR$(65+Y);
440 NEXT
450 FOR Y = 0 TO 9
460 FOR X = 0 TO 9
470 CO = 1 + X * 2 : RO = 1 + Y * 2 : GOSUB 1970 : PRINT CX$;
480 NEXT
490 NEXT
500 CU = 0
510 SZ = 81
520 RO = 0 : CO = LE+1 : GOSUB 1970 : PRINT CX$; : FOR I = 1 TO 8 : PRINT CHR$(CV); : NEXT : PRINT CX$; : FOR I = 1 TO 4 : PRINT CHR$(CV); : NEXT : PRINT CX$;
530 RO = 1 : CO = LE+1 : GOSUB 1970 : PRINT CHR$(CF);"BEKERITO";CHR$(CF);"2023";CHR$(CF);
540 RO = 2 : CO = LE+1 : GOSUB 1970 : PRINT CX$; : FOR I = 1 TO 8 : PRINT CHR$(CV); : NEXT : PRINT CX$; : FOR I = 1 TO 4 : PRINT CHR$(CV); : NEXT : PRINT CX$;
550 RO = 3 : CO = LE : GOSUB 1970 : PRINT CHR$(PL(0));":"; : CO = LE+7 : GOSUB 1970 : PRINT CHR$(PL(1));":"; : CO = LE+14 : GOSUB 1970 : PRINT CHR$(116);":";
560 PO(0)=0: PO(1)=0
570 KI=0
580 KO = 0
590 RO = 3
600 CO = LE+2 : GOSUB 1970 : PRINT PO(0); : CO = LE+9 : GOSUB 1970 : PRINT PO(1); : CO = LE+16 : GOSUB 1970 : PRINT KI;
610 RO = 24 : CO = 0 : GOSUB 1970 : PRINT "MEG";SZ;" BEKERITHETO TERULET VAN";
620 RO = 6 : CO = LE : GOSUB 1970 : PRINT "MOST LEP: ";CHR$(PL(KO));
630 RO = 11: CO = LE : GOSUB 1970 : PRINT "LEPES ";CHR$(ASC("M")+128);"ODOSITASA";
640 IF MK > 0 THEN RO = 13 : CO = LE : GOSUB 1970 : PRINT CHR$(ASC("K")+128);"INCS LETETELE"; : RO = RO+2: CO = LE : GOSUB 1970 : PRINT "KI";CHR$(ASC("N")+128);"CS ELASASA";
650 IF MK = 0 THEN RO = 13 : CO = LE : GOSUB 1970 : PRINT "              "; : RO = RO+2: CO = LE : GOSUB 1970 : PRINT "             ";
660 RO = 19: CO = LE : GOSUB 1970 : PRINT CHR$(ASC("U")+128);"J JATEK";
670 RO = 8 : CO = LE : GOSUB 1970 : PRINT "VONAL (?;?)-(?;?)";
680 CO = LE+7
690 SO = USR(0)
700 IF SO = 85 THEN GOSUB 1850 : IF IN$ = "I" THEN GOTO 360
710 IF SO = 75 AND MK > 0 THEN CH = KK : GOSUB 1680 : GOTO 1790
720 IF SO = 78 AND MK > 0 THEN CH = 32 : GOSUB 1730 :GOTO 1790
730 GOSUB 1910
740 IF SO<65 THEN GOTO 690
750 IF SO>74 THEN GOTO 690
760 GOSUB 1970 : PRINT CHR$(SO);
770 SO=SO-65
780 IF MK > 0 THEN RO = 13 : CO = LE : GOSUB 1970 : PRINT "K"; : RO = RO+2 : CO = LE+2 : GOSUB 1970 : PRINT "N"; : RO = 8 : CO = LE + 7
790 CO = CO+2
800 OS=USR(0) : A=RND(99)
810 IF OS = 77 THEN GOTO 640
820 IF OS = 85 THEN GOSUB 1850 : IF IN$ = "I" THEN GOTO 360
830 GOSUB 1910
840 IF OS<48 THEN GOTO 800
850 IF OS>57 THEN GOTO 800
860 GOSUB 1970 : PRINT CHR$(OS);
870 OS=OS-48
880 RO = SO*2+1 : CO = OS*2+1 : GOSUB 1970 : PRINT KX$;
890 RO = 8 : CO = LE+13
900 VS=USR(0)
910 IF VS = 77 THEN RO = SO*2+1 : CO = OS*2+1 : GOSUB 1970 : PRINT CX$; : GOTO 640
920 IF VS = 85 THEN GOSUB 1850 : IF IN$ = "I" THEN GOTO 360
930 GOSUB 1910
940 IF VS<65 THEN GOTO 900
950 IF VS<65+SO-1 THEN GOTO 900
960 IF VS>74 THEN GOTO 900
970 IF VS>65+SO+1 THEN GOTO 900
980 GOSUB 1970 : PRINT CHR$(VS);
990 VS = VS - 65
1000 CO = CO + 2
1010 VO=USR(0)
1020 IF VO = 77 THEN RO = SO*2+1 : CO = OS*2+1 : GOSUB 1970 : PRINT CX$; : GOTO 640
1030 IF VO = 85 THEN GOSUB 1850 : IF IN$ = "I" THEN GOTO 360
1040 GOSUB 1910
1050 IF VO<48 THEN GOTO 1010
1060 IF VO<48+OS-1 THEN GOTO 1010
1070 IF VO>57 THEN GOTO 1010
1080 IF VO>48+OS+1 THEN GOTO 1010
1090 IF VO=OS AND VS=SO THEN GOTO 1010
1100 VO = VO - 48
1110 X = ( OS + VO ) / 2 : Y = ( SO + VS ) / 2
1120 RM = 49153 + 2 * X + 1 + 40 * ( 2 * Y + 1 )
1130 IF PEEK( RM ) <> 96 THEN GOTO 1010
1140 GOSUB 1970 : PRINT CHR$( VO + 48 );
1150 RO = SO*2+1 : CO = OS*2+1 : GOSUB 1970 : PRINT CX$;
1160 IF SO = VS THEN GOSUB 1380
1170 IF OS = VO THEN GOSUB 1420
1180 IF SZ = 0 THEN GOTO 1210
1190 KO = KO + 1 : IF KO =2 THEN KO = 0
1200 GOTO 590
1210 IF PO(0) = PO(1) THEN GOTO 1340
1220 IF PO(0) > PO(1) THEN NY = 0
1230 IF PO(0) < PO(1) THEN NY = 1
1240 RO = 9 : CO = LE+3 : GOSUB 1970 : PRINT CX$; : FOR I = 1 TO 5 : PRINT CHR$(CV); : NEXT : PRINT CX$;CHR$(CV);CX$;CHR$(CV);CX$;
1250 RO = RO+1 : GOSUB 1970 : PRINT CHR$(CF);"NYERT";CHR$(CF);":";CHR$(CF);CHR$(PL(NY));CHR$(CF);
1260 RO = RO+1 : GOSUB 1970 : PRINT CX$; : FOR I = 1 TO 5 : PRINT CHR$(CV); : NEXT : PRINT CX$;CHR$(CV);CX$;CHR$(CV);CX$;
1270 FOR K = 0 TO 3
1280 RO = 8 - K : CO = LE + 2 - K : GOSUB 1970 : PRINT CX$; : FOR I = 1 TO 11 + 2 * K : PRINT CHR$(CV); : NEXT : PRINT CX$;
1290 FOR RO = 9 - K TO 11 + K : CO = LE + 2 - K : GOSUB 1970 : PRINT CHR$(CF); : CO = LE + 14 + K : GOSUB 1970 : PRINT CHR$(CF); : NEXT
1300 RO = 12 + K : CO = LE + 2 - K : GOSUB 1970 : PRINT CX$; : FOR I = 1 TO 11 + 2 * K : PRINT CHR$(CV); : NEXT : PRINT CX$;
1310 NEXT
1320 A = USR(0) : IF A = 85 THEN GOTO 360
1330 GOTO 1320
1340 RO = 9 : CO = LE+3 : GOSUB 1970 : PRINT CX$; : FOR I = 1 TO 9 : PRINT CHR$(CV); : NEXT : PRINT CX$;
1350 RO = RO+1 : GOSUB 1970 : PRINT CHR$(CF);"DONTETLEN";CHR$(CF);
1360 RO = RO+1 : GOSUB 1970 : PRINT CX$; : FOR I = 1 TO 9 : PRINT CHR$(CV); : NEXT : PRINT CX$;
1370 GOTO 1270
1380 POKE RM, CV
1390 IF ( Y > 0 ) THEN VM = RM-40 : VX = X - .5 : VY = Y - 1 : GOSUB 1460
1400 IF ( Y < 9 ) THEN VM = RM+40 : VX = X - .5 : VY = Y : GOSUB 1460
1410 RETURN
1420 POKE RM, CF
1430 IF ( X > 0 ) THEN VM = RM-1 : VY = Y - .5 : VX = X - 1 : GOSUB 1460
1440 IF ( X < 9 ) THEN VM = RM+1 : VY = Y - .5 : VX = X : GOSUB 1460
1450 RETURN
1460 CN = 0
1470 IF PEEK( VM-40 ) <> 96 THEN CN = CN + 1
1480 IF PEEK( VM+40 ) <> 96 THEN CN = CN + 1
1490 IF PEEK( VM-1 ) <> 96 THEN CN = CN + 1
1500 IF PEEK( VM+1 ) <> 96 THEN CN = CN + 1
1510 MI = VX + 9 * VY
1520 IF CN = 4 THEN POKE VM, PL(KO) : PO(KO) = PO(KO) + 1 + MP( MI ) : SZ = SZ - 1 : GOSUB 1580 : GOSUB 1540
1530 RETURN
1540 IF MP( MI ) = 0 THEN RETURN
1550 FOR I = 1 TO MP( MI ) : GOSUB 1630 : NEXT
1560 MP( MI ) = 0 : KI = KI - 1
1570 RETURN
1580 GOSUB 1960
1590 MA = 60 : HO = 150 : GOSUB 1780
1600 MA = 1 : HO = 150 : GOSUB 1780
1610 GOSUB 1950
1620 RETURN
1630 GOSUB 1960
1640 MA = 50 : HO = 150 : GOSUB 1780
1650 MA = 1 : HO = 150 : GOSUB 1780
1660 GOSUB 1950
1670 RETURN
1680 GOSUB 1960
1690 MA = 45 : HO = 150 : GOSUB 1780
1700 MA = 1 : HO = 150 : GOSUB 1780
1710 GOSUB 1950
1720 RETURN
1730 GOSUB 1960
1740 MA = 130 : HO = 150 : GOSUB 1780
1750 MA = 1 : HO = 150 : GOSUB 1780
1760 GOSUB 1950
1770 RETURN
1780 A = USR( HO*256 + MA ) : RETURN
1790 X = INT( RND( 8.9 ) ) : Y = INT( RND( 8.9 ) )
1800 RM = 49153 + 2*X+2 + 40*(2*Y+2)
1810 V = PEEK( RM )
1820 MI = X + 9 * Y
1830 IF V = 96 THEN POKE RM, CH : KI = KI + 1 : MK = MK - 1 : MP( MI ) = 1 : GOTO 1190
1840 GOTO 1790
1850 CC = CO : CI = 73 : CN = 78 : CM = 49153 + 21 * 40 + LE + 8
1860 RO = 21 : CO = LE : GOSUB 1970 : PRINT "BIZTOS? ";CHR$(CI+128);"/"CHR$(CN+128);
1870 A = USR( 0 ) : POKE CM, CI, 47, CN : CI = CI + 128 : CN = CN + 128 : IF CI > 255 THEN CI = CI -256 : CN = CN - 256
1880 IF A = 73 THEN IN$="I" : RETURN
1890 IF A = 78 THEN IN$="N" : GOSUB 1970 : PRINT "           "; : RO = 8 : CO = CC : RETURN
1900 GOTO 1870
1910 GOSUB 1970 : CU = CU + 1 : IF CU > 9 THEN CU = 0
1920 IF CO = LE + 7 OR CO = LE + 13 THEN PRINT CHR$(65+128+CU) : RETURN
1930 IF CO = LE + 9 OR CO = LE + 15 THEN PRINT CHR$(48+128+CU) : RETURN
1940 PRINT CHR$( 160 ) : RETURN
1950 POKE 16384, 177, 29 : RETURN
1960 POKE 16384, 132, 29 : RETURN
1970 PC = 49153 + RO*40 + CO : HI=INT(PC/256) : LO=PC-256*HI : POKE 16404, LO, HI : RETURN
